command ::= (baos_cmd | service_cmd | monitor_cmd)
baos_cmd ::= (description | get | stored | read | set | put | raw | serveritems | progmode)
service_cmd ::= (name | unname | reset | version | help)
monitor_cmd ::= (watch | unwatch )

BEGIN_ARRAY          ::= WS* #x5B WS*  /* [ left square bracket */
END_ARRAY            ::= WS* #x5D WS*  /* ] right square bracket */
BEGIN_OBJECT         ::= WS* #x7B WS*  /* { left curly bracket */
END_OBJECT           ::= WS* #x7D WS*  /* } right curly bracket */

COMMA      ::= WS* #x2C WS*  /* , comma */
COLON          ::= WS* ":" WS*
Asterisk ::= #x2A
Question ::= #x3F 

WS          ::= [#x20#x09#x0A#x0D]+
EOL         ::= [#x0A#x0D]+

UInt         ::= [0-9]*
UIntArray  ::= BEGIN_ARRAY (UInt WS* (COMMA UInt)*)? END_ARRAY
Number      ::= "-"? ("0" | [1-9] [0-9]*) ("." [0-9]+)? (("e" | "E") ( "-" | "+" )? ("0" | [1-9] [0-9]*))?
String      ::= '"' [^"]* '"' | "'" [^']* "'"
Null        ::= "null"
Bool        ::= "true" | "false"
HexValue             ::= "0x" ([a-fA-F0-9])*

Identifier  ::= ('$$' | '$' | [A-Za-z_])([A-Za-z0-9_]|'.')*

Object               ::= BEGIN_OBJECT (Member (COMMA Member)*)? END_OBJECT
Member               ::= (String | Identifier) COLON Value

Value ::= Number | String | Null | Bool | Object
RawValue ::= String | HexValue | UIntArray

DatapointId ::= ( Asterisk | UInt | Identifier | String )
DatapointValue ::= DatapointId COLON Value
DatapointValueArray  ::= BEGIN_ARRAY (DatapointValue WS* (COMMA DatapointValue)*)? END_ARRAY

DatapointValueRaw ::= DatapointId COLON RawValue
DatapointValueRawArray  ::= BEGIN_ARRAY (DatapointValueRaw WS* (COMMA DatapointValueRaw)*)? END_ARRAY

DatapointIdArray ::= BEGIN_ARRAY (DatapointId WS* (COMMA DatapointId)*) END_ARRAY
DatapointIdList ::= (DatapointId WS*)*

WatchCmdArg ::= ( DatapointId COLON ( String | Identifier ))*
WatchCmdArgArray ::= BEGIN_ARRAY ( WatchCmdArg WS* (COMMA WatchCmdArg)*)? END_ARRAY

DatapointNameArg ::= (UInt COLON ( String | Identifier | Question ))*
DatapointNameArgArray ::= BEGIN_ARRAY ( DatapointNameArg WS* (COMMA DatapointNameArg)*)? END_ARRAY

description ::= "description" WS* ( Asterisk | ( DatapointIdList | DatapointIdArray ))
get ::= "get" WS* ( DatapointIdList | DatapointIdArray )
stored ::= "stored" WS* ( DatapointIdList | DatapointIdArray )
read ::= "read" WS* ( DatapointIdList | DatapointIdArray )
set ::= "set" WS* ( DatapointValue | DatapointValueArray )
raw ::= "raw" WS* ( DatapointValueRaw | DatapointValueRawArray )
put ::= "put" WS* ( DatapointValue | DatapointValueArray )
serveritems ::= "serveritems" WS*
progmode ::= "progmode" WS* ( Question | Bool | Number )

name ::= "name" WS* ( DatapointNameArg | DatapointNameArgArray )
unname ::= "unname" WS* ( DatapointIdList | DatapointIdArray )
reset ::= "reset" WS*
version ::= "version" WS*
help ::= "help" WS*

watch ::= "watch" WS* ( WatchCmdArg | WatchCmdArgArray )
unwatch ::= "unwatch" WS* ( DatapointIdList | DatapointIdArray )
